#include "SDWebServer.h"

/*
  SDWebServer - Example WebServer with SD Card backend for esp8266

  Copyright (c) 2015 Hristo Gochkov. All rights reserved.
  This file is part of the WebServer library for Arduino environment.

  This library is free software; you can redistribute it and/or
  modify it under the terms of the GNU Lesser General Public
  License as published by the Free Software Foundation; either
  version 2.1 of the License, or (at your option) any later version.

  This library is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
  Lesser General Public License for more details.

  You should have received a copy of the GNU Lesser General Public
  License along with this library; if not, write to the Free Software
  Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA

  Have a FAT Formatted SD Card connected to the SPI port of the ESP8266
  The web root is the SD Card root folder
  File extensions with more than 3 characters are not supported by the SD Library
  File Names longer than 8 characters will be truncated by the SD library, so keep filenames shorter
  index.htm is the default index (works on subfolders as well)

  upload the contents of SdRoot to the root of the SDcard and access the editor by going to http://esp8266sd.local/edit
  To retrieve the contents of SDcard, visit http://esp32sd.local/list?dir=/
      dir is the argument that needs to be passed to the function PrintDirectory via HTTP Get request.

*/
#include <WiFi.h>
// #include <NetworkClient.h>
#include <SPIFFS.h>
#include "esp_spiffs.h"
#include <UrlEncode.h>


// const char *ssid = "**********";
// const char *password = "**********";
const char *host = "esp32sd";
// WebServer server(80);

void SDWebServer::returnOK() {
  server.send(200, "text/plain", "");
}

void SDWebServer::returnFail(String msg) {
  server.send(500, "text/plain", msg + "\r\n");
}

bool SDWebServer::loadFromSdCard(String path) {
  String dataType = "text/plain";
  if (path.endsWith("/")) {
    path += "index.htm";
  }

  if (path.endsWith(".src")) {
    path = path.substring(0, path.lastIndexOf("."));
  } else if (path.endsWith(".htm")) {
    dataType = "text/html";
  } else if (path.endsWith(".css")) {
    dataType = "text/css";
  } else if (path.endsWith(".js")) {
    dataType = "application/javascript";
  } else if (path.endsWith(".png")) {
    dataType = "image/png";
  } else if (path.endsWith(".gif")) {
    dataType = "image/gif";
  } else if (path.endsWith(".jpg")) {
    dataType = "image/jpeg";
  } else if (path.endsWith(".ico")) {
    dataType = "image/x-icon";
  } else if (path.endsWith(".xml")) {
    dataType = "text/xml";
  } else if (path.endsWith(".pdf")) {
    dataType = "application/pdf";
  } else if (path.endsWith(".zip")) {
    dataType = "application/zip";
  }

  File dataFile = SD.open(path.c_str());
  if (dataFile.isDirectory()) {
    path += "/index.htm";
    dataType = "text/html";
    dataFile = SD.open(path.c_str());
  }

  if (!dataFile) {
    return false;
  }

  if (server.hasArg("download")) {
    dataType = "application/octet-stream";
  }

  if (server.streamFile(dataFile, dataType) != dataFile.size()) {
    Serial.println("Sent less data than expected!");
  }

  dataFile.close();
  return true;
}

void SDWebServer::handleFileUpload() {

  String dir = server.arg(0);
  HTTPUpload &upload = server.upload();
  
  String path = dir + upload.filename;
  // Serial.println(path);
  if (upload.status == UPLOAD_FILE_START) {
    if (SD.exists(path)) {
      SD.remove(path);
    }
    uploadFile = SD.open(path, FILE_WRITE);
    Serial.print("Upload: START, filename: ");
    Serial.println(upload.filename);
  } else if (upload.status == UPLOAD_FILE_WRITE) {
    if (uploadFile) {
      uploadFile.write(upload.buf, upload.currentSize);
    }
    Serial.print("Upload: WRITE, Bytes: ");
    Serial.println(upload.currentSize);
  } else if (upload.status == UPLOAD_FILE_END) {
    if (uploadFile) {
      uploadFile.close();
    }
    Serial.print("Upload: END, Size: ");
    Serial.println(upload.totalSize);
  }
  // returnOK();
}

void SDWebServer::deleteRecursive(String path) {
  File file = SD.open((char *)path.c_str());
  if (!file.isDirectory()) {
    file.close();
    SD.remove((char *)path.c_str());
    return;
  }

  file.rewindDirectory();
  while (true) {
    File entry = file.openNextFile();
    if (!entry) {
      break;
    }
    String entryPath = path + "/" + entry.name();
    if (entry.isDirectory()) {
      entry.close();
      deleteRecursive(entryPath);
    } else {
      entry.close();
      SD.remove((char *)entryPath.c_str());
    }
    yield();
  }

  SD.rmdir((char *)path.c_str());
  file.close();
}

void SDWebServer::handleDelete() {
  if (!server.hasArg("path")) {
    return returnFail("BAD ARGS");
  }
  String path = server.arg("path");
  if (path == "/" || !SD.exists((char *)path.c_str())) {
    returnFail("BAD PATH");
    return;
  }
  deleteRecursive(path);
  returnOK();
}

void SDWebServer::handleCreate() {
  if (server.args() == 0) {
    return returnFail("BAD ARGS");
  }
  String path = server.arg("path");
  if (path == "/" || SD.exists((char *)path.c_str())) {
    returnFail("BAD PATH");
    return;
  }

  if (path.indexOf('.') > 0) {
    File file = SD.open((char *)path.c_str(), FILE_WRITE);
    if (file) {
      file.write(0);
      file.close();
    }
  } else {
    SD.mkdir((char *)path.c_str());
  }
  returnOK();
}

void SDWebServer::printDirectory() {
  if (!server.hasArg("dir")) {
    return returnFail("BAD ARGS");
  }
  String path = server.arg("dir");
  if (path != "/" && !SD.exists((char *)path.c_str())) {
    return returnFail("BAD PATH");
  }
  File dir = SD.open((char *)path.c_str());
  path = String();
  if (!dir.isDirectory()) {
    dir.close();
    return returnFail("NOT DIR");
  }
  dir.rewindDirectory();
  server.setContentLength(CONTENT_LENGTH_UNKNOWN);
  server.send(200, "text/json", "");

  server.sendContent("[");
  for (int cnt = 0; true; ++cnt) {
    File entry = dir.openNextFile();
    if (!entry) {
      break;
    }

    String output;
    if (cnt > 0) {
      output = ',';
    }

    output += "{\"type\":\"";
    output += (entry.isDirectory()) ? "dir" : "file";
    output += "\",\"name\":\"";
    output += entry.name();
    output += "\"";
    output += "}";
    server.sendContent(output);
    entry.close();
  }
  server.sendContent("]");
  dir.close();
}

void SDWebServer::handleIndex() {
    // Serial.println(spiffsIndexFileStr.c_str());
    String content = String(spiffsIndexFileStr.c_str());
    // Serial.println(spiffsIndexFileStr.c_str());
    server.send(200, "text/html; charset=utf-8", content);
    
    server.sendContent(content);
}

void SDWebServer::handleNotFound() {
  if (hasSD && loadFromSdCard(server.uri())) {
    return;
  }
  String message = "Error \n\n";
  message += "URI: ";
  message += server.uri();
  message += "\nMethod: ";
  message += (server.method() == HTTP_GET) ? "GET" : "POST";
  message += "\nArguments: ";
  message += server.args();
  message += "\n";
  for (uint8_t i = 0; i < server.args(); i++) {
    message += " NAME:" + server.argName(i) + "\n VALUE:" + server.arg(i) + "\n";
  }
  server.send(404, "text/plain", message);
  Serial.print(message);
}

void SDWebServer::handleCreateFolder() {
  if (!server.hasArg("dir")) {
    return returnFail("BAD ARGS");
  }
  String path = server.arg("dir");
  if (SD.exists(path)) {
    server.send(500, "text/json", "{\"code\":0, \"msg\":\"folder exist!\"}");
    return;
  }
  if (SD.mkdir(path)) {
    server.send(200, "text/json", "{\"code\":1}");
  } else {
    server.send(500, "text/json", "{\"code\":0}");
  }
}

void SDWebServer::setup(void) {

  if (MDNS.begin(host)) {
    MDNS.addService("http", "tcp", SDWEBSERVER_PORT);
    Serial.println("MDNS responder started");
    Serial.print("You can now connect to sdwebserver http://");
    Serial.print(host);
    Serial.println(".local");
  }
  
  server.on("/", HTTP_GET, [&]() { handleIndex(); });
  server.on("/list", HTTP_GET,[&]() { printDirectory(); });
  server.on("/delete", HTTP_POST, [&]() { handleDelete(); });
  server.on("/edit", HTTP_PUT, [&]() { handleCreate(); });
  server.on("/upload", HTTP_POST, [&]() { returnOK(); }, [&]() { handleFileUpload(); });
  server.on("/createFolder", HTTP_POST, [&]() { handleCreateFolder(); });
  server.onNotFound([&]() { handleNotFound(); });

  server.begin(SDWEBSERVER_PORT);
  Serial.println("SD WebServer server started");

    if (SD.begin(21)) {
        Serial.println("SD Card initialized.");
        hasSD = true;
    }

    if (!SPIFFS.begin(true)) {
        Serial.println("SPIFFS 初始化失败");
        return;
    }

    // size_t total = 0, used = 0;
    // esp_spiffs_info("", &total, &used);
    // printf("Flash chip size: %d\n", total);
    // printf("Used size: %d\n", used);

    File file = SPIFFS.open("/index.htm", "r");
    if (!file) {
        Serial.println("open spiffs file index.html failure");
        return;
    }

    uint8_t buffer[128] = {0};
    while(file.available()) {
        int count = file.read(buffer, 128);
        std::string s = std::string((char*)buffer, count);
        this->spiffsIndexFileStr.append(s);
        memset(buffer, 0, 128);
    }

}

void SDWebServer::loop(void) {
  server.handleClient();
//   delay(2);  //allow the cpu to switch to other tasks
}
